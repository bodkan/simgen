// Define model constants (to be substituted) all in one place
// (each {{placeholder}} will be replaced by a value passed from R).
// Note that string constant template patterns are surrounded by "quotes"!
initialize() {
    defineConstant("s", {{s}});
    defineConstant("onset_time", {{onset_time}});
    defineConstant("origin_pop", "{{origin_pop}}");

    // compose a trajectory file based on given parameters
    defineConstant("traj_file", PATH + "/" + "trajectory.tsv");
}

// Because we want to simulate non-neutral evolution, we have to provide a
// custom initialization callback -- slendr will use it to replace its default
// neutral genomic architecture (i.e. the initialize() {...} callback it uses
// by default for neutral simulations). Note that we can refer to slendr's
// constants SEQUENCE_LENGTH and RECOMBINATION_RATE, which will carry values
// passed through from R via slendr's slim() R function.
initialize() {
    // initialize a mutation type of a benefitial mutation
    initializeMutationType("m1", 0.5, "f", s);
    initializeGenomicElementType("g1", m1, 1.0);

    // create a single "chromosome"" segment of a length given by slendr
    initializeGenomicElement(g1, 0, SEQUENCE_LENGTH - 1);

    // we won't be simulating neutral mutations in SLiM
    initializeMutationRate(0);

    // but we will simulate recombination at a rate given by slendr
    initializeRecombinationRate(RECOMBINATION_RATE);
}

function (void) add_mutation(void) {
    // sample one target carrier of the new mutation...
    target = sample(population(origin_pop).genomes, 1);
    // ... and add the mutation in the middle of its "chromosome"
    mut = target.addNewDrawnMutation(m1, position = asInteger(SEQUENCE_LENGTH / 2));

    // save the mutation for later reference
    defineGlobal("MUTATION", mut);

    write_log("adding beneficial mutation to population " + origin_pop);

    // write the header of the trajectory file
    writeFile(traj_file, "time\tpop\tfreq\tonset");
}

tick(onset_time) late() {
    // save simulation state in case we need to restart if the mutation is lost
    save_state();

    add_mutation();
}

tick(onset_time):SIMULATION_END late() {
    // the mutation is not segregating and is not fixed either -- we must restart,
    // because this means the simulation has lost it
    if (!MUTATION.isSegregating & !MUTATION.isFixed) {
        write_log("mutation lost (simulation will be restarted)");

        reset_state();

        add_mutation();
    }

    // compute the frequency of the mutation of interest in each population
    // and save it (if the mutation is missing at this time, save its frequency as NA)
    for (pop in c("EHG", "ANA", "EUR", "YAM")) {
        freq = "NA";
        // if a population exists in the current generation, compute allele frequency
        if (population(pop, check = T))
            freq = population(pop).genomes.mutationFrequenciesInGenomes();

        // write the frequency in this population to a trajectory file
        writeFile(traj_file,
                  model_time(community.tick) + "\t" +
                  pop + "\t" +
                  freq + "\t" + onset_time, append = T);
    }
}
